; Script generated by the Inno Setup Script Wizard.

// ISSI addon
#define ISSI_Splash ".\files\arts\twag-splash-screen.bmp"
#define ISSI_Splash_T 3
#define ISSI_Splash_X 960
#define ISSI_Splash_Y 540
#define ISSI_Splash_CORNER 48
// #define ISSI_BackgroundImage_BGColor "$391289"
#define ISSI_BackgroundImage ".\files\arts\twag-installer-bg.bmp"
#define ISSI_IncludePath "C:\ISSI"
#include ISSI_IncludePath+"\_issi.isi"  
// app-verzió változó
#define MyAppID "{AED2502A-9F3E-4441-A4E2-FE0310172C40}"
#define MyAppVersion "23.0206"

[Setup]
; NOTE: The value of AppId uniquely identifies this application.
; Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{#MyAppID}
AppName=The Witcher Adventure Game :: magyarítás
AppVersion={#MyAppVersion}
AppPublisher=KovácsMûhely
AppendDefaultDirName=no
DefaultDirName="{code:GetInstallationPath}"
DirExistsWarning=no
UsePreviousAppDir=no
; DisableProgramGroupPage=yes  
DefaultGroupName=KovácsMûhely
InfoBeforeFile=C:\TWAGINST\files\inftxts\adventuregame-readme-hu-1.txt
InfoAfterFile=C:\TWAGINST\files\inftxts\adventuregame-readme-hu-2.txt
OutputDir=C:\TWAGINST\output
OutputBaseFilename=TheWitcherAdventureGame-magyaritas-v230206-uabe
SetupIconFile=C:\TWAGINST\huninstaller.ico
Compression=lzma2/max
SolidCompression=no
WizardStyle=modern
PrivilegesRequired=lowest

[Languages]
Name: "hungarian"; MessagesFile: "compiler:Languages\Hungarian.isl"

[Icons]
Name: "{group}\Adventure Game magyarítás eltávolítása"; Filename: "{app}\unins001.exe" 

[InstallDelete]
Type: filesandordirs; Name: "{app}\_addons\"
Type: filesandordirs; Name: "{app}\__addons\"

[Files]
// BACKUP original file(s)
Source: "{app}\TheWitcherAdventureGame_Data\resources.assets"; DestDir: "{app}\TheWitcherAdventureGame_Data\"; DestName: "resources.assets.original"; Flags: external skipifsourcedoesntexist onlyifdoesntexist uninsneveruninstall
Source: "{app}\TheWitcherAdventureGame_Data\sharedassets0.assets"; DestDir: "{app}\TheWitcherAdventureGame_Data\"; DestName: "sharedassets0.assets.original"; Flags: external skipifsourcedoesntexist onlyifdoesntexist uninsneveruninstall
Source: "{app}\TheWitcherAdventureGame_Data\sharedassets1.assets"; DestDir: "{app}\TheWitcherAdventureGame_Data\"; DestName: "sharedassets1.assets.original"; Flags: external skipifsourcedoesntexist onlyifdoesntexist uninsneveruninstall
// COPY new files & UABE installer
Source: "C:\TWAGINST\files\addons\*.*"; DestDir: "{app}\_segedletek"; Flags: ignoreversion
Source: "C:\TWAGINST\files\uabes\TWAG-magyaritas-UABE-installer.exe"; DestDir: "{app}\TheWitcherAdventureGame_Data\"; Flags: ignoreversion  

[Run]
Filename: "{app}\TheWitcherAdventureGame_Data\TWAG-magyaritas-UABE-installer.exe"; Description: "Magyarítás alkalmazása";
Filename: "{app}\_segedletek\TheWitcherAdventureGame-jatekszabaly-v200420.pdf"; Description: "Játékszabály elolvasása (PDF)"; Flags: postinstall shellexec skipifsilent unchecked
Filename: "{app}\TheWitcherAdventureGame.exe"; Description: "Játék futtatása"; Flags: postinstall skipifsilent unchecked 

[UninstallDelete]
Type: filesandordirs; Name: "{app}\_segedletek\"
Type: files; Name: "{app}\TheWitcherAdventureGame_Data\resources.assets"
Type: files; Name: "{app}\TheWitcherAdventureGame_Data\sharedassets0.assets"
Type: files; Name: "{app}\TheWitcherAdventureGame_Data\sharedassets1.assets"
Type: files; Name: "{app}\TheWitcherAdventureGame_Data\resources.assets.bak0000"
Type: files; Name: "{app}\TheWitcherAdventureGame_Data\sharedassets0.assets.bak0000"
Type: files; Name: "{app}\TheWitcherAdventureGame_Data\sharedassets1.assets.bak0000"


[Code]


//================================================== REGISTRY alapján a TWAG telepítési útvonalának megkeresés & DEFAULT útvonal deklarálása, ha nem található a telepítési útvonal
var
  InstallationPath: string;

function GetInstallationPath(Param: string): string;
begin
  if InstallationPath = '' then
  begin
    if RegQueryStringValue(
         HKLM32, 'SOFTWARE\WOW6432Node\GOG.com\Games\1207666883',
         'path', InstallationPath) then
    begin
      MsgBox('Siker!' #13 'Megvan a játék GOG-könyvtára itt:' #13 + InstallationPath + #13#13 'Amennyiben mégsem ide szeretnéd telepíteni, úgy lesz még a későbbiekben lehetőséged az útvonal módosítására!', mbError, MB_OK);
    end
      else
    if RegQueryStringValue(
         HKLM64, 'SOFTWARE\Microsoft\Windows\CurrentVersion\Uninstall\Steam App 303800',
         'InstallLocation', InstallationPath) then
    begin
      MsgBox('Siker!' #13 'Megvan a játék Steam-könyvtára itt:' #13 + InstallationPath + #13#13 'Amennyiben mégsem ide szeretnéd telepíteni, úgy lesz még a későbbiekben lehetőséged az útvonal módosítására!', mbError, MB_OK);
    end
      else
    begin
      InstallationPath := 'C:\The Witcher Adventure Game';
      MsgBox('Nem található a játék könyvtára. Manuálisan kell betallóznod a »TheWitcherAdventureGame.exe« fájlt tartalmazó mappát, különben az alapértelmezett könyvtár ez lesz:' #13 + InstallationPath, mbError, MB_OK);
    end;
  end;
  Result := InstallationPath;
end;


//================================================== TheWitcherAdventureGame.exe meglétének vizsgálata az adott könyvtárban
function NextButtonClick(CurPageID: Integer): Boolean;
begin
  Log('NextButtonClick(' + IntToStr(CurPageID) + ') called');
  case CurPageID of
    wpSelectDir:
    if not FileExists(ExpandConstant('{app}\TheWitcherAdventureGame.exe')) then begin
      if MsgBox('Úgy látszik, nem ez lesz a megfelelő könyvtár (ebben a könyvtárban nem található a »TheWitcherAdventureGame.exe« fájl).' #13#13 'Próbáld manuálisan betallózni a merevlemezeden található The Witcher Adventure Game példányod könyvtárát!', mbError, MB_OK) = IDOK then
        begin
          WizardForm.BackButton.OnClick(WizardForm.BackButton);
          Result := False;
        end;
      end;
    wpReady:
  end;
  Result := True;
end;


//================================================== UNINSTALL visszanevezgetések
procedure CurUninstallStepChanged(CurUninstallStep: TUninstallStep);
var
  OldFile1: string;
  OldFile2: string;
  OldFile3: string;
begin
  case CurUninstallStep of usPostUninstall:
  begin
    OldFile1 := ExpandConstant('{app}\TheWitcherAdventureGame_Data\resources.assets.original');
    OldFile2 := ExpandConstant('{app}\TheWitcherAdventureGame_Data\sharedassets0.assets.original');
    OldFile3 := ExpandConstant('{app}\TheWitcherAdventureGame_Data\sharedassets1.assets.original');
    if FileExists(OldFile1) then
      RenameFile(OldFile1, ExpandConstant('{app}\TheWitcherAdventureGame_Data\resources.assets'));
    if FileExists(OldFile2) then
      RenameFile(OldFile2, ExpandConstant('{app}\TheWitcherAdventureGame_Data\sharedassets0.assets'));
    if FileExists(OldFile3) then  
      RenameFile(OldFile3, ExpandConstant('{app}\TheWitcherAdventureGame_Data\sharedassets1.assets'));
    end;
  end;
end;